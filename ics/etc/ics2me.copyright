1i\
.so /home/greg/sw/ics/etc/ics.me
/\\/s//\\\\/g
/^ *$/d
/^%.*$/d
/_[\.a-zA-Z]/s/_\([\.a-zA-Z][\.a-zA-Z_0-9]*\)/_\1_/g
/^!!\([1-9][0-9]*\)  *\(.*\)$/i\
.ce
/^!!\([0-9][0-9]*\)  *\(.*\)$/s||Chapter \1. \2|
/^![0-9]/s|^!\([0-9]\)  *\(.*\)$|.sh \1 "\2"|
/^\*\*\*\*\*  *\(.*\)$/s||.sh 5 "\1"|
/^\*\*\*\*  *\(.*\)$/s||.sh 4 "\1"|
/^\*\*\*  *\(.*\)$/s||.sh 3 "\1"|
/^\*\*  *\(.*\)$/s||.sh 2 "\1"|
/^\*\([1-9][0-9]*\)  *\(.*\)/i\
.ce
/^\*\([1-9][0-9]*\) \(.*\)/s||Chapter \1. \2|
/^o /i\
.B
/^o /s'''
/^- /i\
.D
/^- /s'''
/^: /i\
.L
/^: /s'''
/^+c /i\
.<f\
\
C Code: Copyright (c) 1989 Virtual Infinity Systems.  All Rights Reserved.\

/^+c /a\
.>f
/^+c /s'^+c  *\(.*\)$'{File: \1}\
.so \1'
/^+d /i\
\
.P\
Include graphic diagram from file
/^+d /a\
.P\

/^+d /s/^+d  *\(.*\)$/``\1''./
